buildscript {
    ext {
        nexus_url = "${project.findProperty('nexus_url') ?: System.getenv('NEXUS_URL') ?: System.getenv('NEXUS_HOST')}"
        nexus_user = "${project.findProperty('nexus_user') ?: System.getenv('NEXUS_USERNAME')}"
        nexus_pw = "${project.findProperty('nexus_pw') ?: System.getenv('NEXUS_PASSWORD')}"
        no_nexus = (project.findProperty('no_nexus') ?: System.getenv('NO_NEXUS') ?: false).toBoolean()
        if (!no_nexus && (nexus_url == "null" || nexus_user == "null" || nexus_pw == "null")) {
            throw new GradleException("property no_nexus='false' (or not defined) but at least one of the properties nexus_url, nexus_user or nexus_pw is not configured. Please configure those properties!")
        }

        def folderRel = (String)("${project.findProperty('nexus_folder_releases') ?: System.getenv('NEXUS_FOLDER_RELEASES')}")
        nexusFolderReleases = folderRel == "null" ? "maven-releases" : folderRel

        def folderSnaps = (String)("${project.findProperty('nexus_folder_snapshots') ?: System.getenv('NEXUS_FOLDER_SNAPSHOTS')}")
        nexusFolderSnapshots = folderSnaps == "null" ? "maven-snapshots" : folderSnaps

        snippetsDir = file('build/generated-snippets')
    }
}

plugins {
  id 'java'
  id 'groovy'
  id 'com.adarshr.test-logger' version "2.0.0"
}

repositories {
    if (no_nexus) {
        println("using repositories 'jcenter' and 'mavenCentral', because property no_nexus=$no_nexus")
        jcenter()
        mavenCentral()
    } else {
        println("using nexus repositories")
        maven() {
            url "${nexus_url}/repository/jcenter/"
            credentials {
                username = "${nexus_user}"
                password = "${nexus_pw}"
            }
        }

        maven() {
            url "${nexus_url}/repository/maven-public/"
            credentials {
                username = "${nexus_user}"
                password = "${nexus_pw}"
            }
        }

        maven() {
            url "${nexus_url}/repository/atlassian_public/"
            credentials {
                username = "${nexus_user}"
                password = "${nexus_pw}"
            }
        }
    }
}

ext {
    junitVersion = "5.5.2"
    spockVersion = "1.3-groovy-2.5"
    gebVersion = "3.2"
    seleniumVersion = "3.141.59"
    htmlunitVersion = "2.36.0"
    unirestVersion = "3.2.00"
    // When a test fail we can continue or fail the stage
    CONTINUE_WHEN_TEST_FAIL = true
}

dependencies {
    testCompile "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile "com.konghq:unirest-java:${unirestVersion}"
    testCompile "org.gebish:geb-spock:${gebVersion}"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
    testCompile "org.seleniumhq.selenium:htmlunit-driver:${htmlunitVersion}"
    testRuntime "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
}

sourceSets {
    installation {
        groovy {
            srcDir 'src/test/installation/groovy'
        }
        java {
            srcDir 'src/test/installation/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
    }

    integration {
        groovy {
            srcDir 'src/test/integration/groovy'
        }
        java {
            srcDir 'src/test/integration/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
    }

    acceptance {
        groovy {
            srcDir 'src/test/acceptance/groovy'
        }
        java {
            srcDir 'src/test/acceptance/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntime
    }
}

configurations {
    installationCompile.extendsFrom testCompile
    installationRuntime.extendsFrom testRuntime

    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime

    acceptanceCompile.extendsFrom testCompile
    acceptanceRuntime.extendsFrom testRuntime
}

class TestExecutionPhases {
    static final String INSTALLATION = "installation"
    static final String INTEGRATION = "integration"
    static final String ACCEPTANCE = "acceptance"
}

class TestLanguages {
    static final String JAVA = "java"
    static final String GROOVY = "groovy"
}

def generateTaskName(def type, def language) {
    return "${type}-${language}"
}

// Task to create in a parametrized way Tests task
def executeTest(def type, def language) {
    return tasks.create(generateTaskName(type, language), Test) {
        description = "Runs ${type} tests ${language}."
        group = "verification"

        if ("${language}".equalsIgnoreCase(TestLanguages.JAVA)) {
            // Mandatory to run JUnit 5 test but incompatible with groovy tests
            useJUnitPlatform()
        }

        ignoreFailures = "${CONTINUE_WHEN_TEST_FAIL}"
        testClassesDirs = sourceSets["${type}"].output.classesDirs
        classpath = sourceSets["${type}"].runtimeClasspath
    }
}

test {
    // To create the tasks
    dependsOn executeTest(TestExecutionPhases.INSTALLATION, TestLanguages.JAVA)
    dependsOn executeTest(TestExecutionPhases.INSTALLATION, TestLanguages.GROOVY)
    dependsOn executeTest(TestExecutionPhases.INTEGRATION, TestLanguages.JAVA)
    dependsOn executeTest(TestExecutionPhases.INTEGRATION, TestLanguages.GROOVY)
    dependsOn executeTest(TestExecutionPhases.ACCEPTANCE, TestLanguages.JAVA)
    dependsOn executeTest(TestExecutionPhases.ACCEPTANCE, TestLanguages.GROOVY)

    // To define the order
    tasks.findByName(generateTaskName(TestExecutionPhases.ACCEPTANCE, TestLanguages.GROOVY)).mustRunAfter generateTaskName(TestExecutionPhases.ACCEPTANCE, TestLanguages.JAVA)
    tasks.findByName(generateTaskName(TestExecutionPhases.ACCEPTANCE, TestLanguages.JAVA)).mustRunAfter generateTaskName(TestExecutionPhases.INTEGRATION, TestLanguages.GROOVY)
    tasks.findByName(generateTaskName(TestExecutionPhases.INTEGRATION, TestLanguages.GROOVY)).mustRunAfter generateTaskName(TestExecutionPhases.INTEGRATION, TestLanguages.JAVA)
    tasks.findByName(generateTaskName(TestExecutionPhases.INTEGRATION, TestLanguages.JAVA)).mustRunAfter generateTaskName(TestExecutionPhases.INSTALLATION, TestLanguages.GROOVY)
    tasks.findByName(generateTaskName(TestExecutionPhases.INSTALLATION, TestLanguages.GROOVY)).mustRunAfter generateTaskName(TestExecutionPhases.INSTALLATION, TestLanguages.JAVA)
}

testlogger {
    showStandardStreams true
}
